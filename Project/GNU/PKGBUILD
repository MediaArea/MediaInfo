# $Id$
# Maintainer: MediaArea.net SARL <info@mediaarea.net>
# Contributor: hydro <hydro@freenet.de>

pkgname=('mediainfo' 'mediainfo-gui' 'mediainfo-nautilus-plugin' 'mediainfo-nemo-plugin')
pkgver=19.09
pkgrel=1
pkgdesc="supplies technical and tag information about a video or audio file"
url="http://mediaarea.net"
license=('GPL')
source=(${pkgname}_${pkgver}.orig.tar.xz)
md5sums=('00000000000000000000000000000000')
arch=('i686' 'x86_64')
depends=('libzen>=0.4.37' 'libmediainfo>=19.09' 'wxgtk' 'libsm' 'desktop-file-utils')

prepare() {
    cd "${srcdir}"/MediaInfo/Project/GNU/CLI
    sh ./autogen.sh
    ./configure --prefix=/usr

    cd "${srcdir}"/MediaInfo/Project/GNU/GUI
    sh ./autogen.sh
    ./configure --prefix=/usr
}

build() {
    cd "${srcdir}"/MediaInfo/Project/GNU/CLI
    make

    cd "${srcdir}"/MediaInfo/Project/GNU/GUI
    make
}

package_mediainfo() {
    pkgdesc="${pkgdesc} (CLI)"
    depends=('libmediainfo')

    cd "${srcdir}"/MediaInfo/Project/GNU/CLI
    make DESTDIR="${pkgdir}" install
}

package_mediainfo-gui() {
    pkgdesc="${pkgdesc} (GUI)"
    depends=('libmediainfo' 'wxgtk' 'libsm' 'desktop-file-utils')

    cd "${srcdir}"/MediaInfo/Project/GNU/GUI
    make DESTDIR="${pkgdir}" install
}

package_mediainfo-nautilus-plugin() {
    pkgdesc="${pkgdesc} (Nautilus plugin)"
    depends=('python-mediainfo' 'python-nautilus')

    install -m 0755 -d ${pkgdir}/usr/share/nautilus-python/extensions/mediainfo/ui/
    install -m 0755 -d ${pkgdir}/usr/share/nautilus-python/extensions/mediainfo/locale/
    install -m 0644 "${srcdir}"/MediaInfo/Source/Resource/Plugin/Language/*.csv ${pkgdir}/usr/share/nautilus-python/extensions/mediainfo/locale/
    install -m 0644 "${srcdir}"/MediaInfo/Source/Plugins/FMS/ui.glade ${pkgdir}/usr/share/nautilus-python/extensions/mediainfo/ui/
    install -m 0644 "${srcdir}"/MediaInfo/Source/Plugins/FMS/__init__.py ${pkgdir}/usr/share/nautilus-python/extensions/mediainfo/
    install -m 0644 "${srcdir}"/MediaInfo/Source/Plugins/FMS/tab.py ${pkgdir}/usr/share/nautilus-python/extensions/mediainfo/
    install -m 0644 "${srcdir}"/MediaInfo/Source/Plugins/FMS/nautilus-mediainfo.py ${pkgdir}/usr/share/nautilus-python/extensions/
}

package_mediainfo-nemo-plugin() {
    pkgdesc="${pkgdesc} (Nemo plugin)"
    depends=('python-mediainfo' 'nemo-python')

    install -m 0755 -d ${pkgdir}/usr/share/nemo-python/extensions/mediainfo/ui/
    install -m 0755 -d ${pkgdir}/usr/share/nemo-python/extensions/mediainfo/locale/
    install -m 0644 "${srcdir}"/MediaInfo/Source/Resource/Plugin/Language/*.csv ${pkgdir}/usr/share/nemo-python/extensions/mediainfo/locale/
    install -m 0644 "${srcdir}"/MediaInfo/Source/Plugins/FMS/ui.glade ${pkgdir}/usr/share/nemo-python/extensions/mediainfo/ui/
    install -m 0644 "${srcdir}"/MediaInfo/Source/Plugins/FMS/__init__.py ${pkgdir}/usr/share/nemo-python/extensions/mediainfo/
    install -m 0644 "${srcdir}"/MediaInfo/Source/Plugins/FMS/tab.py ${pkgdir}/usr/share/nemo-python/extensions/mediainfo/
    install -m 0644 "${srcdir}"/MediaInfo/Source/Plugins/FMS/nemo-mediainfo.py ${pkgdir}/usr/share/nemo-python/extensions/
}
